# Public route table
resource "aws_route_table" "public" {
  vpc_id = var.vpc_id
  
  tags = {
    Name        = "${var.rt_name}-public-rt"
    Type        = "public"
  }
}

# Public route to Internet Gateway
resource "aws_route" "public_internet_access" {
  route_table_id         = aws_route_table.public.id
  destination_cidr_block = "0.0.0.0/0"
  gateway_id             = var.internet_gateway_id
}

# Public route table associations
resource "aws_route_table_association" "public" {
  count          = length(var.public_subnet_ids)
  subnet_id      = var.public_subnet_ids[count.index]
  route_table_id = aws_route_table.public.id
}

# Private route tables (one per AZ)
resource "aws_route_table" "private" {
  count  = var.az_count
  vpc_id = var.vpc_id
  
  tags = {
    Name        = "${var.rt_name}-private-rt-${count.index + 1}"
  }
}

# Private route to NAT Gateway
resource "aws_route" "private_nat_gateway" {
  count                  = var.az_count
  route_table_id         = aws_route_table.private[count.index].id
  destination_cidr_block = "0.0.0.0/0"
  nat_gateway_id = var.single_nat_gateway ? var.nat_gateway_ids[0] : var.nat_gateway_ids[count.index]

}

# Private route table associations
resource "aws_route_table_association" "private" {
  count          = var.az_count
  subnet_id      = var.private_subnet_ids[count.index]
  route_table_id = aws_route_table.private[count.index].id
}